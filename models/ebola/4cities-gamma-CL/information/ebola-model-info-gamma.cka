%use:
#Parameters
%var: 'infected-transmission' 0.25
%var: 'dead-transmission' 0.2
%var: 'case-fatality-proportion' 0.7

## Good!
%var: 'exposure-stages' 6.0
%var: 'exposure-time' 11.0
%var: 'exposure-rate' 'exposure-stages'/'exposure-time'

## Redo the death and infectious process, following the pattern above!
%var: 'Dead_contagious-time' 4.0
%var: 'Infected-time' 6.0

#Agents
%agent: Person(c~S~E~I, info~zero~neg~pos,d)
%agent: Dead(d~I~R)
%agent: Removed()
%agent: Day(i1,i2)

#Vars
%var: 'S' Person(c~S)
%var: 'E' Person(c~E)
%var: 'I' Person(c~I)
%var: 'R' Removed()
%var: 'D' Dead()
%var: 'N' 'S'+'E'+'I'+'R'
%var: 'infectious_live' 'infected-transmission'/'N' 
%var: 'infectious_dead' 'dead-transmission'/'N'

#Transmission and information
%var: 'S_0 I_0_rate' 'infectious_live'
%var: 'S_0 I_+_rate' 'infectious_live'/10
%var: 'S_0 I_-_rate' 'infectious_live'*1.5
%var: 'S_- I_0_rate' 'infectious_live'*1.5
%var: 'S_- I_+_rate' 'infectious_live'/5
%var: 'S_- I_-_rate' 'infectious_live'*2.0
%var: 'S_+ I_0_rate' 'infectious_live'
%var: 'S_+ I_+_rate' 'infectious_live'/15
%var: 'S_+ I_-_rate' 'infectious_live'

%var: 'S_0 D_rate' 'infectious_dead'
%var: 'S_+ D_rate' 'infectious_dead'/10
%var: 'S_- D_rate' 'infectious_dead'*1.5

#Information Gov
%var: 'SEI inform+_rate' 0.0
%var: 'SEI inform-_rate' 0.05
%var: 'SEI inform- +_rate' 0.00
%var: 'I inform_0+_rate' 0.1
%var: 'I inform_0-_rate' 0.005

#Information Pear-to-Pear
%var: 'p+ p0_rate' 0.05 
%var: 'p- p0_rate' 0.05 
%var: 'p+ p-_rate' 0.005
%var: 'p- p+_rate' 0.001


##########
# RULES #
########
# Write down your assumptions clearly and confront whether you like them
# Better names for these events
# No _numbers_

#Transmission and information
'S_0 I_0' Person(c~S,d,info~zero),Person(c~I,info~zero) -> Person(c~E,info~zero,d!1),Person(c~I,info~zero),Day(i1!1) @* 'S_0 I_0_rate'
'S_0 I_+' Person(c~S,d,info~zero),Person(c~I,info~pos) -> Person(c~E,info~zero,d!1),Person(c~I,info~pos),Day(i1!1) @* 'S_0 I_+_rate'
'S_0 I_-' Person(c~S,d,info~zero),Person(c~I,info~neg) -> Person(c~E,info~zero,d!1),Person(c~I,info~neg),Day(i1!1) @* 'S_0 I_-_rate'

'S_- I_0' Person(c~S,d,info~neg),Person(c~I,info~zero) -> Person(c~E,d!1,info~neg),Person(c~I,info~zero),Day(i1!1) @* 'S_- I_0_rate'
'S_- I_+' Person(c~S,d,info~neg),Person(c~I,info~pos) -> Person(c~E,d!1,info~neg),Person(c~I,info~pos),Day(i1!1) @* 'S_- I_+_rate'
'S_- I_-' Person(c~S,d,info~neg),Person(c~I,info~neg) -> Person(c~E,d!1,info~neg),Person(c~I,info~neg),Day(i1!1) @* 'S_- I_-_rate'

'S_+ I_0' Person(c~S,d,info~pos),Person(c~I,info~zero) -> Person(c~E,d!1,info~pos),Person(c~I,info~zero),Day(i1!1) @* 'S_+ I_0_rate'
'S_+ I_+' Person(c~S,d,info~pos),Person(c~I,info~pos) -> Person(c~E,d!1,info~pos),Person(c~I,info~pos),Day(i1!1) @* 'S_+ I_+_rate'
'S_+ I_-' Person(c~S,d,info~pos),Person(c~I,info~neg) -> Person(c~E,d!1,info~pos),Person(c~I,info~neg),Day(i1!1) @* 'S_+ I_-_rate'


'S_0 D' Person(c~S,d,info~zero),Dead(d~I) -> Person(c~E,d!1,info~zero),Dead(d~I),Day(i1!1) @* 'S_0 D_rate'
'S_+ D' Person(c~S,d,info~pos),Dead(d~I) -> Person(c~E,d!1,info~pos),Dead(d~I),Day(i1!1) @* 'S_+ D_rate'
'S_- D' Person(c~S,d,info~neg),Dead(d~I) -> Person(c~E,d!1,info~neg),Dead(d~I),Day(i1!1) @* 'S_- D_rate'

#Time
'replication' Day(i1!_,i2) -> Day(i1!_,i2!1),Day(i1!1,i2) @ 'exposure-rate'

'infect' Person(c~E,d!0),Day(i1!0,i2!1),Day(i1!1,i2!2),Day(i1!2,i2!3),Day(i1!3,i2!4),Day(i1!4,i2!5),Day(i1!5,i2!6),Day(i1!6,i2) ->\
			Person(c~I,d) @ [inf]

'Die' Person(c~I) -> Dead(d~I) @ 'case-fatality-proportion'/'Infected-time'
'live' Person(c~I) -> Removed() @ (1-'case-fatality-proportion')/'Infected-time'

'bury' Dead(d~I) -> Dead(d~R) @ 1/'Dead_contagious-time' 

#Information Gov
'SEI inform+' Person(info~zero) -> Person(info~pos) @ 'SEI inform+_rate'
'SEI inform-' Person(info~zero) -> Person(info~neg) @ 'SEI inform-_rate'
'SEI inform- +' Person(info~neg) -> Person(info~pos) @ 'SEI inform- +_rate'
'I inform_0+' Person(c~I,info~zero) -> Person(c~I,info~pos) @ 'I inform_0+_rate'
'I inform_0-' Person(c~I,info~zero) -> Person(c~I,info~neg) @ 'I inform_0-_rate'

#Information Pear-to-Pear
'p+ p0' Person(info~pos),Person(info~zero) -> Person(info~pos),Person(info~pos) @ 'p+ p0_rate'
'p- p0' Person(info~neg),Person(info~zero) -> Person(info~neg),Person(info~neg) @ 'p- p0_rate' 
'p+ p-' Person(info~pos),Person(info~neg) -> Person(info~pos),Person(info~pos) @ 'p+ p-_rate'
'p- p+' Person(info~neg),Person(info~pos) -> Person(info~neg),Person(info~neg) @ 'p- p+_rate'



#Clean
'remove days' Day(i1) ->  @ [inf]
