%use:
#Parameters
%var: 'infected-transmission' 0.25
%var: 'dead-transmission' 0.2
%var: 'case-fatality-proportion' 0.7

## Exposure has a gamma distribution
%var: 'exposure-time' 11.0
%var: 'exposure-stages' 6
%var: 'exposure-rate' 'exposure-stages'/'exposure-time'

## Death and infection have exponential distributions
### Maybe should change to gamma matching above?
%var: 'Dead_contagious-time' 4.0
%var: 'Infected-time' 6.0

#Agents
#%agent: Person(c~S~E~I, info~zero~neg~pos,d)
%agent: Person(c~S~E~I~R, info~zero~neg~pos,d)
%agent: Dead(d~I~R)
#%agent: Removed()
%agent: Day(i1,i2)

#Vars
%var: 'S' Person(c~S)
%var: 'E' Person(c~E)
%var: 'I' Person(c~I)
%var: 'R' Person(c~R)
#%var: 'R' Removed()
%var: 'D' Dead()
%var: 'N' 'S'+'E'+'I'+'R'
%var: 'infectious_live' 'infected-transmission'/'N' 
%var: 'infectious_dead' 'dead-transmission'/'N'
%var: 'Info-pos' Person(info~pos)
%var: 'Info-zero' Person(info~zero)
%var: 'Info-neg' Person(info~neg)


##################
# RATES INFECTION#
#################
#Conversion rates of the original "infectious_live"
#when added information
%var: 'zero-info' 0.1
%var: 'zero-noinfo' 1.5
%var: 'noinfo-info' 0.2
%var: 'noinfo-noinfo' 2.0
%var: 'info-info' 0.06

#Conversion rates of original"infectious_dead"           
#when added information
%var: 'info-dead' 0.1
%var: 'noinfo-dead' 1.5


# Transmission rates for different information groups
%var: 'S_0 I_0_rate' 'infectious_live'
%var: 'S_0 I_+_rate' 'infectious_live'*'zero-info'#0.1
%var: 'S_0 I_-_rate' 'infectious_live'*'zero-noinfo'#1.5
%var: 'S_- I_0_rate' 'infectious_live'*'zero-noinfo'#1.5
%var: 'S_- I_+_rate' 'infectious_live'*'noinfo-info' #0.2
%var: 'S_- I_-_rate' 'infectious_live'*'noinfo-noinfo'#2.0
%var: 'S_+ I_0_rate' 'infectious_live'*'zero-info'
%var: 'S_+ I_+_rate' 'infectious_live'*'info-info'#0.06
%var: 'S_+ I_-_rate' 'infectious_live'*'noinfo-info'

%var: 'S_0 D_rate' 'infectious_dead'
%var: 'S_+ D_rate' 'infectious_dead'*'info-dead' #0.1
%var: 'S_- D_rate' 'infectious_dead'*'noinfo-dead'#1.5

###################
#RATES INFORMATION#
##################
#Information Gov
%var: 'SEI inform+_rate' 0.00 #0
%var: 'SEI inform-_rate' 0.00 #0.05 this variable change a lot the simulation
%var: 'SEI inform- +_rate' 0.00

#Information to infected people
%var: 'I inform_0+_rate' 0.1
%var: 'I inform_0-_rate' 0.005

#Information Peer-to-Peer
%var: 'p+ p0_rate' 0.05 
%var: 'p- p0_rate' 0.05 
%var: 'p+ p-_rate' 0.005
%var: 'p- p+_rate' 0.001

#Information of recovered people
%var: 'live_pos-info'   0.9
%var: 'live_zero-info'  0.05 
%var: 'live_neg-info'   0.05

##########
# RULES #
##########
# Write down your assumptions clearly and confront whether you like them
# Better names for these events
# No _numbers_

## Transmission with information ##
# infected to susceptible
'S_0 I_0' Person(c~S,d,info~zero),Person(c~I,info~zero) -> Person(c~E,info~zero,d!1),Person(c~I,info~zero),Day(i1!1) @* 'S_0 I_0_rate'
'S_0 I_+' Person(c~S,d,info~zero),Person(c~I,info~pos) -> Person(c~E,info~zero,d!1),Person(c~I,info~pos),Day(i1!1) @* 'S_0 I_+_rate'
'S_0 I_-' Person(c~S,d,info~zero),Person(c~I,info~neg) -> Person(c~E,info~zero,d!1),Person(c~I,info~neg),Day(i1!1) @* 'S_0 I_-_rate'

'S_- I_0' Person(c~S,d,info~neg),Person(c~I,info~zero) -> Person(c~E,d!1,info~neg),Person(c~I,info~zero),Day(i1!1) @* 'S_- I_0_rate'
'S_- I_+' Person(c~S,d,info~neg),Person(c~I,info~pos) -> Person(c~E,d!1,info~neg),Person(c~I,info~pos),Day(i1!1) @* 'S_- I_+_rate'
'S_- I_-' Person(c~S,d,info~neg),Person(c~I,info~neg) -> Person(c~E,d!1,info~neg),Person(c~I,info~neg),Day(i1!1) @* 'S_- I_-_rate'

'S_+ I_0' Person(c~S,d,info~pos),Person(c~I,info~zero) -> Person(c~E,d!1,info~pos),Person(c~I,info~zero),Day(i1!1) @* 'S_+ I_0_rate'
'S_+ I_+' Person(c~S,d,info~pos),Person(c~I,info~pos) -> Person(c~E,d!1,info~pos),Person(c~I,info~pos),Day(i1!1) @* 'S_+ I_+_rate'
'S_+ I_-' Person(c~S,d,info~pos),Person(c~I,info~neg) -> Person(c~E,d!1,info~pos),Person(c~I,info~neg),Day(i1!1) @* 'S_+ I_-_rate'

#dead to susceptible
'S_0 D' Person(c~S,d,info~zero),Dead(d~I) -> Person(c~E,d!1,info~zero),Dead(d~I),Day(i1!1) @* 'S_0 D_rate'
'S_+ D' Person(c~S,d,info~pos),Dead(d~I) -> Person(c~E,d!1,info~pos),Dead(d~I),Day(i1!1) @* 'S_+ D_rate'
'S_- D' Person(c~S,d,info~neg),Dead(d~I) -> Person(c~E,d!1,info~neg),Dead(d~I),Day(i1!1) @* 'S_- D_rate'

## end transmission with information ##

####################
# INFORMATION RULES#
###################
#Information from government
'SEI inform+' Person(info~zero) -> Person(info~pos) @ 'SEI inform+_rate'
'SEI inform-' Person(info~zero) -> Person(info~neg) @ 'SEI inform-_rate'
'SEI inform- +' Person(info~neg) -> Person(info~pos) @ 'SEI inform- +_rate'

#Information from government to infected people
'I inform_0+' Person(c~I,info~zero) -> Person(c~I,info~pos) @ 'I inform_0+_rate'
'I inform_0-' Person(c~I,info~zero) -> Person(c~I,info~neg) @ 'I inform_0-_rate'

#Information Peer-to-Peer
'p+ p0' Person(info~pos),Person(info~zero) -> Person(info~pos),Person(info~pos) @ 'p+ p0_rate'
'p- p0' Person(info~neg),Person(info~zero) -> Person(info~neg),Person(info~neg) @ 'p- p0_rate' 
'p+ p-' Person(info~pos),Person(info~neg) -> Person(info~pos),Person(info~pos) @ 'p+ p-_rate'
'p- p+' Person(info~neg),Person(info~pos) -> Person(info~neg),Person(info~neg) @ 'p- p+_rate'

#########
# Time #
########
'replication' Day(i1!_,i2) -> Day(i1!_,i2!1),Day(i1!1,i2) @ 'exposure-rate'
'infect' Person(c~E,d!0),Day(i1!0,i2!1),Day(i1!1,i2!2),Day(i1!2,i2!3),Day(i1!3,i2!4),Day(i1!4,i2!5),Day(i1!5,i2!6),Day(i1!6,i2) ->\
			Person(c~I,d) @ [inf]
'Die' Person(c~I) -> Dead(d~I) @ 'case-fatality-proportion'/'Infected-time'
#Should consider a removed person with information, because they still have
#positive or negative information after they recover
#'live' Person(c~I) -> Removed() @ (1-'case-fatality-proportion')/'Infected-time'
'live-pos' Person(c~I) -> Person(c~R,info~pos) @ (1-'case-fatality-proportion')/'Infected-time' * 'live_pos-info'
'live-zero' Person(c~I) -> Person(c~R,info~zero) @ (1-'case-fatality-proportion')/'Infected-time' * 'live_zero-info'
'live-neg' Person(c~I) -> Person(c~R,info~neg) @ (1-'case-fatality-proportion')/'Infected-time' * 'live_neg-info'

'bury' Dead(d~I) -> Dead(d~R) @ 1/'Dead_contagious-time' 

#Clean
'remove days' Day(i1) ->  @ [inf]
